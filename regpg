#!/usr/bin/perl

use warnings;
use strict;

use File::Temp qw(tempfile);
use Getopt::Std;
use MIME::Base64;

sub usage {
	print STDERR <<EOF;
usage:	regpg help
	regpg addkey [options] <keyname>...
	regpg delkey [options] <keyname>...
	regpg lskeys [options]
	regpg genpwd [options] <cryptfile>
	regpg encrypt [options] [clearfile] <cryptfile>
	regpg decrypt [options] <cryptfile> [clearfile]
	regpg recrypt [options] <cryptfile>...
	regpg lscrypt [options] <cryptfile>
	regpg find [options]
options:
	-a			recrypt all files after keyring changes
	-k pubring.gpg		recipient keyring
	-n			no changes (implies -v)
	-v			verbose mode
Either or both file arguments to `encrypt` and `decrypt`
can be '-' meaning stdin or stdout.
EOF
	exit 1;
}

sub help {
	exec "perldoc -F $0";
}

our %opt;
our @gpg;
our @gpg_de;
our @gpg_en;

sub getargs {
	my $argmin = shift;
	my $argmax = shift;
	usage unless getopts '-hak:nv', \%opt;
	help if $opt{h};
	$opt{k} //= './pubring.gpg';
	$opt{v} = 1 if $opt{n};
	usage if @ARGV < $argmin;
	usage if defined $argmax and @ARGV > $argmax;
	@gpg = (qw(gpg --no-default-keyring --keyring),$opt{k});
	@gpg_de = (@gpg, qw(--use-agent --batch --quiet --decrypt));
	@gpg_en = (@gpg, qw(--trust-model=always --armor --encrypt));
}

sub getout {
	return ('--output', $ARGV[0]) if $ARGV[0] ne '-';
}

sub getinout {
	my $in = shift @ARGV;
	return getout, $in eq '-' ? () : $in;
}

sub verbose {
	print STDERR "@_\n" if $opt{v};
}

sub vexec {
	verbose "running @_";
	exit if $opt{n};
	exec @_;
}

sub vsystem_warn {
	verbose "running @_";
	return if $opt{n};
	system @_;
	warn "failed: @_\n" if $?;
}

sub pipeslurp {
	verbose "pipe from @_";
	return if $opt{n};
	open my $pipe, '-|', @_
	    or die "open pipe from $_[0]: $!\n";
	my @out = <$pipe>;
	unless (close $pipe) {
		die "read pipe from $_[0]: $!\n" if $!;
		die "pipe from $_[0] failed\n";
	}
	return @out;
}

sub pipespew {
	my $data = shift;
	verbose "pipe to @_";
	return if $opt{n};
	open my $pipe, '|-', @_
	    or die "open pipe to $_[0]: $!\n";
	print $pipe $data;
	unless (close $pipe) {
		die "write pipe to $_[0]: $!\n" if $!;
		die "pipe to $_[0] failed\n";
	}
}

sub recipients {
	my $save = $opt{n}; undef $opt{n};
	my @keys = map m{^(?:[^:]*:){4}([^:]*):},
	    grep m{^pub:},
	    pipeslurp @gpg, qw(--list-keys --with-colons);
	$opt{n} = $save;
	return map { ('--recipient' => $_) } @keys;
}

# save stdout once
open my $stdout, '>&STDOUT' or die "dup: $!\n";

sub recrypt_one {
	my $fn = shift;
	my @recipients = @_;
	my $cleartext = join '', pipeslurp @gpg_de, $fn;
	if ($opt{n}) {
		verbose "pipe to", @gpg_en, @recipients,
		    '--output', $fn;
		return;
	}
	my ($th,$tn) = tempfile "$fn.XXXXXXXX";
	verbose "tempfile $tn";
	open STDOUT, '>&', $th or die "dup: $!\n";
	pipespew $cleartext, @gpg_en, @recipients;
	open STDOUT, '>&', $stdout or die "dup: $!\n";
	verbose "rename $tn => $fn";
	rename $tn => $fn
	    or die "rename $tn => $fn: $!\n";
}

# subcommands

sub addkey {
	getargs 1;
	# Export from the user's normal gpg setup. export-minimal
	# removes extraneous signatures from the key, which avoids
	# complaints about unavailable public keys when it is imported
	my $keys = join '',
	    pipeslurp qw(gpg --export --armor
			 --export-options export-minimal),
			     @ARGV;
	# Import to this local keyring
	pipespew $keys, @gpg, '--import';
	return 0;
}

sub delkey {
	getargs 1;
	# --expert persuades gpg to delete the key even if the secret
	# key is available, when deleting one of the user's own keys
	vexec @gpg, '--expert', '--delete-key', @ARGV;
}

sub lskeys {
	getargs 0, 0;
	vexec @gpg, '--fingerprint';
}

sub genpwd {
	getargs 1, 1;
	open my $fh, '<', '/dev/urandom'
	    or die "open /dev/urandom: $!\n";
	my $len = 15;
	$len == sysread $fh, my $bytes, $len
	    or die "read /dev/urandom: $!\n";
	my $chars = encode_base64 $bytes;
	pipespew $chars, @gpg_en, recipients, getout;
	return 0;
}

sub encrypt {
	getargs 1, 2;
	unshift @ARGV, '-' if @ARGV == 1;
	vexec @gpg_en, recipients, getinout;
}

sub decrypt {
	getargs 1, 2;
	push @ARGV, '-' if @ARGV == 1;
	vexec @gpg_de, getinout;
}

sub recrypt {
	getargs 1;
	my @recipients = recipients;
	recrypt_one $_, @recipients for @ARGV;
	return 0;
}

sub lscrypt {
	getargs 1, 1;
	my @keys = grep s{^.*ENC_TO\s+(\S+)\s+.*\n}{$1},
	    pipeslurp @gpg, qw(--list-only --status-fd 1), @ARGV;
	vsystem_warn @gpg, '--fingerprint', $_ for @keys;
	return 0;
}

sub find {
	getargs 0;
	vexec 'find . -type f | xargs grep -l "^-----BEGIN PGP MESSAGE-----$"';
}

usage unless @ARGV;
my $subcommand = shift;
if (exists $::{$subcommand}) {
	exit $::{$subcommand}();
} else {
	usage;
}

__END__

=head1 NAME

regpg - repeatable encryption / decryption for managed set of recipients

=head1 SYNOPSIS

B<regpg> B<help>

B<regpg> B<addkey> [I<options>] <I<keyname>>...

B<regpg> B<delkey> [I<options>] <I<keyname>>...

B<regpg> B<lskeys> [I<options>]

B<regpg> B<genpwd> [I<options>] <I<cryptfile>>

B<regpg> B<encrypt> [I<options>] [I<clearfile>] <I<cryptfile>>

B<regpg> B<decrypt> [I<options>] <I<cryptfile>> [I<clearfile>]

B<regpg> B<recrypt> [I<options>] <I<cryptfile>>...

B<regpg> B<lscrypt> [I<options>] <I<cryptfile>>

B<regpg> B<find>

=head1 DESCRIPTION

The B<regpg> program helps with managing secrets that need to be
stored encrypted in revision control for a configuration management
system.
These secrets need to be decrypted only in controlled circumstances.

You use the B<regpg> B<*keys> subcommands to maintain a GPG public key
ring file (by default F<./pubring.gpg>) which lists the set of people
who can decrypt the secrets.

You use the B<regpg> B<*crypt> subcommands to manage encrypted files.

=head1 OPTIONS

The B<regpg> subcommands all take the same options.

=over

=item B<-a>

For the B<addkey>, B<delkey>, and B<recrypt> subcommands,
recrypt all files found by the B<find> subcommand.

=item B<-k> I<pubring.gpg>

Specify the name of the public key ring file,
to override the default F<./pubring.gpg>.

=item B<-n>

Do nothing, but show what would have been done.

=item B<-v>

Verbose mode. This mainly prints the GPG commands.

=back

=head1 SUBCOMMANDS

=over

=item B<regpg> B<help>

Display this documentation.

=item B<regpg> B<addkey> <I<keyname>>...

Export keys from your default GPG public keyring,
and import them into the B<regpg> keyring.

A I<keyname> can be a key fingerprint or ID
or a person's email address.

=item B<regpg> B<delkey> <I<keyname>>...

Delete keys from the B<regpg> keyring.

A I<keyname> can be a key fingerprint or ID
or a person's email address.

=item B<regpg> B<lskeys>

List the keys in the B<regpg> keyring.

=item B<regpg> B<genpwd> <I<cryptfile>>

Generate a 20 character password, encrypt it, and store it in I<cryptfile>.

If I<cryptfile> is C<-> then it is written to stdout.

=item B<regpg> B<encrypt> [I<clearfile>] <I<cryptfile>>

Encrypt I<clearfile> to produce I<cryptfile>.

If I<clearfile> is C<-> or is omitted then it is read from stdin.

If I<cryptfile> is C<-> then it is written to stdout.

=item B<regpg> B<decrypt> <I<cryptfile>> [I<clearfile>]

Decrypt I<cryptfile> to produce I<clearfile>.

If I<cryptfile> is C<-> then it is read from stdin.

If I<clearfile> is C<-> or is omitted then it is written to stdout.

You must be running C<gpg-agent> which will be used
to gain access to your private key for decryption.

=item B<regpg> B<recrypt> <I<cryptfile>>...

Re-encrypt I<cryptfile>s.

You should use this after using B<addkey> or B<delkey>.

=item B<regpg> B<lscrypt> <I<cryptfile>>

List the keys which are able to decrypt I<cryptfile>.

=item B<regpg> B<find>

Find and list encrypted files.

=back

=head1 ANSIBLE VAULT

You can use B<regpg> to manage your B<ansible-vault> password.

Create a vault password:

	$ regpg genpwd vault.pwd

Create a decryption script:

	$ cat >vault.sh
	#!/bin/sh
	regpg decrypt vault.pwd -
	^D
	$ chmod +x vault.sh

To set up automatic decryption, edit F<ansible.cfg> to add

	[default]
	vault_password_file = ./vault.sh

=head1 ANSIBLE WITHOUT VAULT

Accompanying B<regpg> is an Ansible Jinja2 filter plugin called
B<gpg_de> that decrypts secrets for deployment on servers.

To set up the B<gpg_de> filter plugin, copy it into a F<plugins/filter>
subdirectory, next to your B<roles> subdirectory.
Edit F<ansible.cfg> to add

	[default]
	filter_plugins = plugins/filter

Ansible filter plugins work in Jinja2 templates. For example,

	- name: install ssh host keys
	  copy:
	    dest: /etc/ssh/{{item}}
	    content: "{{ item+'.asc' | gpg_de }}"
	  with_items
	    - ssh_host_dsa_key
	    - ssh_host_ecdsa_key
	    - ssh_host_ed25519_key
	    - ssh_host_rsa_key

=head1 SEE ALSO

gpg(1), gpg-agent(1)

=cut
